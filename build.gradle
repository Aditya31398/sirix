apply from: "${rootDir}/libraries.gradle"

allprojects {
    group = 'io.sirix'
    version = '0.9.4-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    java {
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_13
        targetCompatibility = JavaVersion.VERSION_13
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from(components.java)
            }
        }
    }

    configurations.all {
    }

    configurations {
        testJar
    }

    task testsJar(type: Jar) {
        archiveClassifier = 'tests'
        from(sourceSets.test.output)
    }

    artifacts {
        archives testsJar
    }

    jar {
        into("META-INF/maven/io.sirix/$project.name") {
            from { generatePomFileForMavenPublication }
            rename ".*", "pom.xml"
        }
    }

    publishing.publications.maven.artifact(testsJar)

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        useTestNG()
    }
}
